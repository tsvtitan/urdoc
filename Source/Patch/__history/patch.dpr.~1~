program patch;

uses Windows,SysUtils;

const
   IMAGE_RESOURCE_DATA_IS_DIRECTORY = $80000000;
   IMAGE_OFFSET_STRIP_HIGH          = $7FFFFFFF;
   SwitchMessage='m';

type

   PCheckPoint=^TCheckPoint;
   TCheckPoint=packed record
     dwOemId: DWord;
     wProcessorArchitecture: Word;
     dwPageSize: DWord;
     dwActiveProcessorMask: DWord;
     dwNumberOfProcessors: DWord;
     dwProcessorType: DWord;
     dwAllocationGranularity: DWord;
     wProcessorLevel: Word;
     wProcessorRevision: Word;
     dwWin32Platform: DWord;
     dwWin32MajorVersion: DWord;
     dwWin32MinorVersion: DWord;
     dwWin32BuildNumber: DWord;
     Win32CSDVersion: array[0..127] of AnsiChar;
     dwVolumeSerialNumber: DWord;
     CPUSpeed: Extended;
   end;

  PIMAGE_RESOURCE_DIRECTORY_ENTRY=^IMAGE_RESOURCE_DIRECTORY_ENTRY;
  IMAGE_RESOURCE_DIRECTORY_ENTRY=packed record
    Name: DWord;
    OffsetToData: DWord;
  end;

  PIMAGE_RESOURCE_DIRECTORY=^IMAGE_RESOURCE_DIRECTORY;
  IMAGE_RESOURCE_DIRECTORY=packed record
    Characteristics: DWord;
    TimeDateStamp: DWord;
    MajorVersion: Word;
    MinorVersion: Word;
    NumberOfNamedEntries: Word;
    NumberOfIdEntries: Word;
  end;

  PIMAGE_RESOURCE_DATA_ENTRY = ^IMAGE_RESOURCE_DATA_ENTRY;
  IMAGE_RESOURCE_DATA_ENTRY = packed record
    OffsetToData    : DWORD;
    Size            : DWORD;
    CodePage        : DWORD;
    Reserved        : DWORD;
  end;

  PIMAGE_RESOURCE_DIR_STRING_U = ^IMAGE_RESOURCE_DIR_STRING_U;
  IMAGE_RESOURCE_DIR_STRING_U = packed record
    Length          : WORD;
    NameString      : array [0..0] of WCHAR;
  end;

var
  HFile: THandle;
  hFileMap: Thandle;
  FileMapView: Pointer=nil;

procedure FreeFileMap;
begin
 if HFile<>0 then begin
  if hFileMap<>0 then begin
    if FileMapView<>nil then
     UnmapViewOfFile(FileMapView);
    CloseHandle(hFileMap);
    hFileMap:=0;
    FileMapView:=nil;
  end;
  CloseHandle(hFile);
  hFile:=0;
 end;
end;

function CreateFileMap(Value: String): Boolean;
begin
  FreeFileMap;
  result:=false;
  hFile := CreateFile(Pchar(Value), GENERIC_READ+GENERIC_WRITE, FILE_SHARE_READ+FILE_SHARE_WRITE, nil,
                        OPEN_EXISTING, FILE_ATTRIBUTE_NORMAL, 0);
  if hFile = INVALID_HANDLE_VALUE then exit;

  hFileMap:=CreateFileMapping(hFile,nil,PAGE_READWRITE,0,0,nil);
  if (HFileMap=INVALID_HANDLE_VALUE) or (HFileMap=0) then begin
    CloseHandle(hFile);
  end;

  FileMapView:=MapViewOfFile(hFileMap,FILE_MAP_ALL_ACCESS,0,0,0);
  if FileMapView=nil then begin
    CloseHandle(hFileMap);
    CloseHandle(hFile);
    hFileMap:=0;
    HFile:=0;
    exit;
  end;
  Result:=true;
end;

function GetCPUSpeed: Extended;
const
 DelayTime = 100;
var
 TimerHi, TimerLo: DWORD;
 PriorityClass, Priority: Integer;
begin
  PriorityClass := GetPriorityClass(GetCurrentProcess);
  Priority := GetThreadPriority(GetCurrentThread);
  SetPriorityClass(GetCurrentProcess, REALTIME_PRIORITY_CLASS);
  SetThreadPriority(GetCurrentThread,
  THREAD_PRIORITY_TIME_CRITICAL);
  Sleep(10);
  asm
    dw 310Fh // rdtsc
    mov TimerLo, eax
    mov TimerHi, edx
  end;
  Sleep(DelayTime);
  asm
    dw 310Fh // rdtsc
    sub eax, TimerLo
    sbb edx, TimerHi
    mov TimerLo, eax
    mov TimerHi, edx
  end;
  SetThreadPriority(GetCurrentThread, Priority);
  SetPriorityClass(GetCurrentProcess, PriorityClass);
  Result := TimerLo / (1000.0 * DelayTime);
end;

procedure GetCheckPoint(FileName: string; var Check: TCheckPoint);
var
  SI: TSystemInfo;
  lpRootPathName: PChar;
  lpVolumeNameBuffer: array [0..MAX_PATH] of Char;
  nVolumeNameSize: DWORD;
  lpVolumeSerialNumber: PDWORD;
  lpMaximumComponentLength: DWORD;
  lpFileSystemFlags: DWORD;
begin
  GetSystemInfo(SI);
  Check.dwOemId:=SI.dwOemId;
  Check.wProcessorArchitecture:=SI.wProcessorArchitecture;
  Check.dwPageSize:=SI.dwPageSize;
  Check.dwActiveProcessorMask:=SI.dwActiveProcessorMask;
  Check.dwNumberOfProcessors:=SI.dwNumberOfProcessors;
  Check.dwProcessorType:=SI.dwProcessorType;
  Check.dwAllocationGranularity:=SI.dwAllocationGranularity;
  Check.wProcessorLevel:=SI.wProcessorLevel;
  Check.wProcessorRevision:=SI.wProcessorRevision;
  Check.dwWin32Platform:=Win32Platform;
  Check.dwWin32MajorVersion:=Win32MajorVersion;
  Check.dwWin32MinorVersion:=Win32MinorVersion;
  Check.dwWin32BuildNumber:=Win32BuildNumber;
  StrCopy(Check.Win32CSDVersion,Pchar(Win32CSDVersion));

  lpRootPathName:=Pchar(ExtractFiledrive(FileName)+'\');
  nVolumeNameSize:=DWord(Sizeof(lpVolumeNameBuffer));
  FillChar(lpVolumeNameBuffer,nVolumeNameSize,0);
  lpVolumeNameBuffer[0] := #$00;
  new(lpVolumeSerialNumber);
  try
   if GetVolumeInformation(lpRootPathName,lpVolumeNameBuffer,nVolumeNameSize,
                              lpVolumeSerialNumber,lpMaximumComponentLength,
                              lpFileSystemFlags, nil,0) then
   Check.dwVolumeSerialNumber:=DWord(lpVolumeSerialNumber^);
  finally
    dispose(lpVolumeSerialNumber);
  end;
  Check.CPUSpeed:=GetCPUSpeed;
end;

procedure CryptCheckPoint(Addr: DWord; Size: DWord; Crypt: Boolean);
var
  i: DWord;
  b: Byte;
  j: Integer;
begin
  j:=0;
  for i:=Addr to Addr+Size-1 do begin
   move(Pointer(i)^,b,1);
   if Crypt then begin
    if Odd(j) then  begin
     b:=b+j*3;
    end else begin
     b:=b-j*4;
    end;
   end else begin
    if Odd(j) then  begin
     b:=b-j*3;
    end else begin
     b:=b+j*4;
    end;
   end;
   move(b,Pointer(i)^,1);
   inc(j);
  end;
end;

procedure GetMemPatch(HeadRes,PatchRes: String; var MemPatch: Pointer; var SizePatch: Integer);

    function GetPointer(mHandle: DWord; Addr: DWord; StartAddress: DWord;
                        NumSection: Word; var Delta: Integer):Pointer;
    var
     i: Word;
     SecHeader: TImageSectionHeader;
    begin
     Result:=Pointer(mHandle+Addr);
     Delta:=0;
     for i:=0 to NumSection-1 do begin
       MOve(Pointer(StartAddress)^,SecHeader,Sizeof(TImageSectionHeader));
       StartAddress:=StartAddress+Sizeof(TImageSectionHeader);
       if (SecHeader.VirtualAddress<=Addr)and
          (SecHeader.VirtualAddress+SecHeader.SizeOfRawData>Addr) then begin
         Result:=Pointer(mHandle+Addr-SecHeader.VirtualAddress+SecHeader.PointerToRawData);
         Delta:=-SecHeader.VirtualAddress+SecHeader.PointerToRawData;
         exit;
       end;
     end;
    end;

    function GetResourceTypeFromId(TypeID: Word): string;
    begin
     Result:='Unknown ('+inttostr(TypeID)+')';
     case TypeID of
        $2000: Result:='NEWRESOURCE'; // RT_NEWRESOURCE
        $7FFF: Result:='ERROR'; // RT_ERROR
        1: Result:='CURSOR'; // RT_CURSOR
        2: Result:='BITMAP'; // RT_BITMAP
        3: Result:='ICON'; // RT_ICON
        4: Result:='MENU'; // RT_MENU
        5: Result:='DIALOG'; // RT_DIALOG
        6: Result:='STRING'; // RT_STRING
        7: Result:='FONTDIR'; // RT_FONTDIR
        8: Result:='FONT'; // RT_FONT
        9: Result:='ACCELERATORS'; // RT_ACCELERATORS
        10: Result:='RCDATA'; // RT_RCDATA
        11: Result:='MESSAGETABLE'; // RT_MESSAGETABLE
        12: Result:='GROUP CURSOR'; // RT_GROUP_CURSOR
        14: Result:='GROUP ICON'; // RT_GROUP_ICON
        16: Result:='VERSION'; // RT_VERSION
        2 or $2000: Result:='NEWBITMAP'; // RT_BITMAP|RT_NEWRESOURCE
        4 or $2000: Result:='NEWMENU'; // RT_MENU|RT_NEWRESOURCE
        5 or $2000: Result:='NEWDIALOG'; // RT_DIALOG|RT_NEWRESOURCE
     end;
    end;

    function WideCharToStr(WStr: PWChar; Len: Integer): string;
    begin
     if Len = 0 then Len := -1;
     Len := WideCharToMultiByte(CP_ACP, 0, WStr, Len, nil, 0, nil, nil);
     SetLength(Result, Len);
     WideCharToMultiByte(CP_ACP, 0, WStr, Len, PChar(Result), Len, nil, nil);
    end;

    function StripHighBit(L: DWord): Dword;
    begin
     Result := L and IMAGE_OFFSET_STRIP_HIGH;
    end;

    function HighBitSet(L: DWord): Boolean;
    begin
     Result := (L and IMAGE_RESOURCE_DATA_IS_DIRECTORY) <> 0;
    end;

    function GetResourceNameFromId(ResourceBase: DWord;
                      resDirEntry: PIMAGE_RESOURCE_DIRECTORY_ENTRY): string;
    var
     PDirStr: PIMAGE_RESOURCE_DIR_STRING_U;
    begin
     PDirStr := PIMAGE_RESOURCE_DIR_STRING_U(StripHighBit(resDirEntry.Name)+ResourceBase);
     Result:=WideCharToStr(@PDirStr.NameString, PDirStr.Length);
    end;

    function GetNameResource(resType,ResourceBase: DWord;
                     resDirEntry: PIMAGE_RESOURCE_DIRECTORY_ENTRY;
                     level: Integer): string;
    begin
     if resDirEntry=nil then begin
      result:=GetResourceTypeFromId(resType);
      exit;
     end;

     if not HighBitSet(resDirEntry.Name) and  (resDirEntry.Name <= 16)and (level<1) then
     begin
      Result := GetResourceTypeFromId(resType);
      Exit;
     end;

    if HighBitSet(resDirEntry.Name) then
    begin
     Result :=GetResourceNameFromId(ResourceBase,resDirEntry);
     Exit;
    end;
    Result := Format('%d', [resDirEntry.Name]);
   end;

var
  Delta: Integer;
  isFind: Boolean;

      procedure FindResource( resDir: PIMAGE_RESOURCE_DIRECTORY;
                             resourceBase: DWord;
                             Level: Integer;
                             TypeParentRes: Word;
                             IsTypeParentName: Boolean;
                             ParentNameRes: string);

         procedure FindResourcePlus(resDirEntry: PIMAGE_RESOURCE_DIRECTORY_ENTRY);
         var
          resType: DWord;
          resDataEntry: PIMAGE_RESOURCE_DATA_ENTRY;
          RDataE: PIMAGE_RESOURCE_DATA_ENTRY;
          NameRes: string;
          TypeParentName: Boolean;
         begin
          if Level=0 then
           resType:=resDirEntry.Name
          else resType:=TypeParentRes;
          NameRes:=GetNameResource(resType,resourceBase,resDirEntry,level);
          if NameRes=PatchRes then begin
            isFind:=true;
          end;
          if Level=0 then
           ParentNameRes:=NameRes;
          if not HighBitSet(resDirEntry.Name) and
                 (resDirEntry.Name <= 16)then begin
           TypeParentName:=false;
          end else begin
           if (Level>0)and(not IsTypeParentName)then begin
            TypeParentName:=false;
           end else begin
            TypeParentName:=true;
           end;
          end;

          if HighBitSet(resDirEntry.OffsetToData) then begin
            FindResource(Pointer(resourceBase+StripHighBit(resDirEntry.OffsetToData)),
                         resourceBase,Level+1,resType,TypeParentName,ParentNameRes);
          end else begin
           if (ParentNameRes=HeadRes)and(isFind) then begin
            resDataEntry:=Pointer(resourceBase+StripHighBit(resDirEntry.OffsetToData));
            RDataE:=PIMAGE_RESOURCE_DATA_ENTRY(resDataEntry);
            MemPatch:=Pointer(Integer(FileMapView)+Integer(RDataE.OffsetToData)+Delta);
            SizePatch:=RDataE.Size;
            isFind:=false;
           end;
          end;
         end;

      var
        resDirEntry: PIMAGE_RESOURCE_DIRECTORY_ENTRY;
        i: DWord;
      begin
       resDirEntry:=PIMAGE_RESOURCE_DIRECTORY_ENTRY(resDir);
       inc(PIMAGE_RESOURCE_DIRECTORY(resDirEntry));

       if resDir.NumberOfNamedEntries>0 then
        for i:=0 to resDir.NumberOfNamedEntries-1 do begin
         FindResourcePlus(resDirEntry);
         inc(resDirEntry);
        end;

       if resDir.NumberOfIdEntries>0 then
        for i:=0 to resDir.NumberOfIdEntries-1 do begin
         FindResourcePlus(resDirEntry);
         inc(resDirEntry);
        end;
      end;

var
  VirtualAddress: DWord;
//  Size: DWord;
  DosHeader: TImageDosHeader;
  NtHeader: TImageNtHeaders;
  OptionalHeader: TImageOptionalHeader;
  FileHeader: TImageFileHeader;
  SectionAddress,NumberOfSections: DWord;
  IRD: PIMAGE_RESOURCE_DIRECTORY;
begin
  Move(FileMapView^,DosHeader,sizeof(TImageDosHeader));
  if DosHeader.e_magic<>IMAGE_DOS_SIGNATURE then exit;
  Move(Pointer(DWord(FileMapView)+DWord(DosHeader._lfanew))^,NtHeader,sizeof(TImageNtHeaders));
  Move(NtHeader.FileHeader,FileHeader,sizeof(TImageFileHeader));
  Move(NtHeader.OptionalHeader,OptionalHeader,sizeof(TImageOptionalHeader));
  VirtualAddress:=OptionalHeader.DataDirectory[IMAGE_DIRECTORY_ENTRY_RESOURCE].VirtualAddress;
//  Size:=OptionalHeader.DataDirectory[IMAGE_DIRECTORY_ENTRY_RESOURCE].Size;
  SectionAddress:=DWord(FileMapView)+DWord(DosHeader._lfanew)+sizeof(TImageNtHeaders);
  NumberOfSections:=FileHeader.NumberOfSections;
  IRD:=GetPointer(DWord(FileMapView),VirtualAddress,SectionAddress,NumberOfSections,Delta);
  if IsBadCodePtr(IRD) then exit;
  try
   isFind:=false;
   FindResource(IRD,DWord(IRD),0,0,false,'');
  except
  end; 
end;

function PatchMainFile(FileName: string): Boolean;
var
  MemPatch: Pointer;
  SizePatch: Integer;
  Check: TCheckPoint;
const
  PatchRes='CHECKPOINT';
  HeadRes='BITMAP';
begin
  Result:=false;
  if not CreateFileMap(FileName) then exit;
  try
   MemPatch:=nil;
   SizePatch:=0;
   GetMemPatch(HeadRes,PatchRes,MemPatch,SizePatch);
   if MemPatch<>nil then begin
    if sizeof(TCheckPoint)<=SizePatch then begin
      FillChar(Check,sizeof(TCheckPoint),0);
      GetCheckPoint(FileName,Check);
      CryptCheckPoint(DWord(@Check),SizeOf(TCheckPoint),true);
      CryptCheckPoint(DWord(@Check),SizeOf(TCheckPoint),false);
      CryptCheckPoint(DWord(@Check),SizeOf(TCheckPoint),true);

      move(Check,MemPatch^,Sizeof(TCheckPoint));

      Result:=true;
    end;
   end;
  finally
    FreeFileMap;
  end;
end;

var
  s,s1: string;
begin
  if ParamCount>0 then begin
    s:=ParamStr(1);
    if FileExists(s) then begin
      if not PatchMainFile(s) then
        ExitCode:=1;
    end else begin
      s:=ExtractFilePath(ParamStr(0))+s;
      if FileExists(s) then begin
        if not PatchMainFile(s) then
          ExitCode:=1;
      end else ExitCode:=1;
    end;
    if ParamCount>1 then begin
      s1:=ParamStr(2);
      if AnsiSameText(s1,SwitchMessage) then begin
        if ExitCode>0 then begin
          MessageBox(0,PChar('Обновление прошло неудачно.'),PChar('Ошибка'),MB_OK+MB_ICONERROR);
        end else begin
          MessageBox(0,PChar('Обновление прошло успешно.'),PChar('Информация'),MB_OK+MB_ICONINFORMATION);
        end;
      end;
    end;
  end;
end.
